// This object is used in conjunction with the electrode controller
// allowing for the user to create any type of electrode easily

begintemplate Electrode
    public calcVoltage              // these are two different ways of calculating extracellular voltage
    public calcVoltageCompartment   // the compartment version is made to be more efficient

    public setAmp       // setters for various electrode params    
    public setFreq  
    public setDur
    public setDel
    public setPos
    public movPos
    public oppPolarity

    public getAmp       // getters for various electrode params
    public getFreq
    public getDur
    public getDel
    public getXPos
    public getYPos
    public getModelNum

    public printData    // output data
    public printDataAdvanced

    public electrodeModel   // the model is public knowledge
    public modelNum

    external axontotal  // external variables needed
    external rhoe       //  these are used during the distance calculation
    external x          //   (see setPos())

    create electrodeSection // all point processes need to be within a section
    objref electrodeModel
    objref  ext // store the distance calculation for the electrdoes
    index = -1
    xpos = -1
    ypos = -1
    public polarity
    public amplitude
    
    proc init(){
        // electrodes are created with 4 parameters
        index = $1      // the index of the electrode
        modelNum = $2   // model type 0=Sine, 1=Triangle, 2=Square, 3=Sine Ramp, 4=DCSine, 5=DC
        xpos = $3
        ypos = $4
        polarity = 1

        if (modelNum==0){
            electrodeSection{
                electrodeModel = new IClampSine(.5)
                electrodeModel.amp = 1000000
                electrodeModel.f = 10000
                electrodeModel.offset = 0
                electrodeModel.del = 0
                electrodeModel.dur = 300
            }
            amplitude = 1000000
        }else if (modelNum==1){
            electrodeSection{
                electrodeModel = new IClampTri(.5)
                electrodeModel.amp = 1000000
                electrodeModel.freq = 10000
                electrodeModel.offset = 0
                electrodeModel.del = 0
                electrodeModel.dur = 300
            }
            amplitude = 1000000
        }else if (modelNum==2){
            electrodeSection{
                electrodeModel = new Bi_trainIClamp(.5)
                electrodeModel.del = 10
                electrodeModel.train = 100
                electrodeModel.cathod_dur = .1
                electrodeModel.postcathod_dur = 0
                electrodeModel.anod_dur = .1
                electrodeModel.postanod_dur = 0
                electrodeModel.high_amp = -300000
            }
            amplitude = -300000
        }else if (modelNum==3){
            electrodeSection{
                electrodeModel = new SineRamp(.5)             
            }
        }else if (modelNum==5){
            electrodeSection{
                electrodeModel = new Bi_trainIClamp(.5)
                electrodeModel.del = 10
                electrodeModel.train = 100
                electrodeModel.cathod_dur = 100
                electrodeModel.postcathod_dur = 0
                electrodeModel.anod_dur = 0
                electrodeModel.postanod_dur = 0
                electrodeModel.high_amp = -300000
            }
            amplitude = -300000
        }
        setPos()
    }

    func calcVoltage(){ 
        // calculate the voltage at a particular point from the electrode
        x = $1
        y = $2
        return electrodeModel.i * (rhoe/(4*PI*sqrt((ypos-y)^2 + (xpos-x)^2))) *1e-6
    }
    
    func calcVoltageCompartment(){
        // calculates the voltage for a particular compartment (using the indexing in x)
        //  this is faster than using the calcVoltage
        //  the index is passed as a parameter
        return electrodeModel.i * ext.x[$1] 
    }

    proc setPos(){ local i
        // do the calculation for all distances to the compartments ahead of time
        // optionally can be used to set the new position of the electrodes by passing x and y as arguments
        if(numarg() == 2){
            xpos = $1
            ypos = $2
        }
        ext = new Vector(axontotal,0)
        for i=0, axontotal-1{
            ext.x[i] = (rhoe)/(4*PI*sqrt((ypos^2)+((xpos-x.x[i])^2)))*1e-6	//nV//
        }
    }

    proc movPos(){
        // change to pos by the amounts passed (change in x, change in y)
        xpos = xpos + $1
        ypos = ypos + $2
        setPos()
    }

    proc setAmp(){
        // sets the amplitude of the electrode depending on the electrode type
        // the amplitude in nA is passed as the only parameter
        if(numarg() == 1){
            amplitude = $1 * polarity
        }
        
        if (modelNum==0){   // sine electrode
            electrodeModel.amp = amplitude
        }else if(modelNum==1){  // tri electrode
            electrodeModel.amp = amplitude
        }else if(modelNum==2){  // square electrode
            electrodeModel.high_amp = amplitude
            electrodeModel.low_amp  = amplitude
        }else if(modelNum==5){  // DC electrode
            electrodeModel.high_amp = amplitude
        }
    }

    proc setFreq(){
        // sets the frequency of the elctrode which depends on the electrode type
        // the frequency in Hz is the only parameter
        if (modelNum==0){   // sine Electrode
            electrodeModel.f = $1
        }else if(modelNum==1){  // tri electrode
            electrodeModel.freq = $1
        }else if(modelNum==2){  // square electrode
            electrodeModel.cathod_dur = 1/($1*1000*2)   // half period in ms
            electrodeModel.postcathod_dur = 0
            electrodeModel.anod_dur = 1/($1*1000*2)
            electrodeModel.postanod_dur = 0
        }else if(modelNum==5){  // DC electrode
            print "ERROR: CAN NOT SET FREQUENCY OF DC ELECTRODE"
        }
    }

    proc setDur(){
        // sets the duration of the elctrode which depends on the electrode type
        // the duration in ms is the only parameter
        if (modelNum==0){   // sine Electrode
            electrodeModel.dur = $1
        }else if(modelNum==1){  // tri electrode
            electrodeModel.dur = $1
        }else if(modelNum==2){  // square electrode
            electrodeModel.train = $1
        }else if(modelNum==5){  // DC electrode
            electrodeModel.train = $1
        }
    }

    proc setDel(){
        // sets the delay of the elctrode which depends on the electrode type
        // the delay in ms is the only parameter
        if (modelNum==0){   // sine Electrode
            electrodeModel.del = $1
        }else if(modelNum==1){  // tri electrode
            electrodeModel.del = $1
        }else if(modelNum==2){  // square electrode
            electrodeModel.del = $1
        }else if(modelNum==5){  // DC electrode
            electrodeModel.del = $1
        }
    }

    proc oppPolarity(){
        // flips the polarity of the electrode (neg becomes pos, pos becomes neg)
        polarity = polarity*-1
        amplitude = amplitude * -1
        setAmp()    // sets the amplitude to the new value based on the new polarity
    }
    
    func getAmp(){
        // getter for amp value (nA)
        if (modelNum==0){   // sine electrode
            return electrodeModel.amp
        }else if(modelNum==1){  // tri electrode
            return electrodeModel.amp
        }else if(modelNum==2){  // square electrode
            return electrodeModel.high_amp
        }else if(modelNum==5){  // DC electrode
            return electrodeModel.high_amp
        }
        return -1   // if unknown modelNum
    }

    func getFreq(){
        // getter for freq value (Hz)
        if (modelNum==0){   // sine Electrode
            return electrodeModel.f
        }else if(modelNum==1){  // tri electrode
            return electrodeModel.freq
        }else if(modelNum==2){  // square electrode
            // the frequency must be calculated for square electrodes
            return 1/(electrodeModel.cathod_dur*2/1000)
        }else if(modelNum==5){  // DC electrode
            // print "ERROR: CAN NOT GET FREQUENCY OF DC ELECTRODE"
            return 0
        }
        return -1   // if unknown modelNum
    }

    func getDur(){
        // getter for duration value (ms)
        if (modelNum==0){   // sine Electrode
            return electrodeModel.dur
        }else if(modelNum==1){  // tri electrode
            return electrodeModel.dur
        }else if(modelNum==2){  // square electrode
            return electrodeModel.train
        }else if(modelNum==5){  // DC electrode
            return electrodeModel.train
        }
        return -1   // if unknown modelNum
    }

    func getDel(){
        // getter for delay value (ms)
        if (modelNum==0){   // sine Electrode
            return electrodeModel.del
        }else if(modelNum==1){  // tri electrode
            return electrodeModel.del
        }else if(modelNum==2){  // square electrode
            return electrodeModel.del
        }else if(modelNum==5){  // DC electrode
            return electrodeModel.del
        }
        return -1   // if unknown modelNum
    }

    func getXPos(){
        // getter for x position
        return xpos
    }

    func getYPos(){
        // getter for y position
        return ypos
    }

    func getModelNum(){
        // getter for modelNum
        return modelNum
    }

    proc printData(){
        print "Index, ", index
        print "Electrode Model, ", electrodeModel
        print "xpos, ypos, ", xpos, ",", ypos
    }    
    
    proc printDataAdvanced(){
        print "Index, ", index
        print "Electrode Model, ", electrodeModel
        print "xpos, ypos, ", getXPos(), ",", getYPos()
        print "Amplitude, ", getAmp()
        print "Frequency, ", getFreq()
        print "Duration, ", getDur()
        print "Delay, ", getDel()

    }
endtemplate Electrode