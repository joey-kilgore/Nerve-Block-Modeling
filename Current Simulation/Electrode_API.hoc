proc electrode_api_advance() { local cur1, cur2, cur3
    // Sets the extracellular voltage created by the current electrode(s)
    // electrode_api_advance(v1, [v2, [v3]])

    cur1= $1
    if(numarg() > 1){
        cur2 = $2
        if(numarg() > 2){
            cur3 = $3
        } else {
            cur3 = 0
        }
    } else {
        cur2 = 0
        cur3 = 0
    }

    a = 0
    for (i=0;i<=axontotal-1;i=i+stinnum+5) {	
        node[a].e_extracellular(0.5)=(cur1*ext1.x[i]*1e-6)+ (cur2*ext2.x[i]*1e-6)+ (cur3*ext3.x[i]*1e-6) //mV//
        a = a+1
    }

    b=0 
    for (i=1;i<=axontotal-1;i=i+stinnum+5) {
        MYSA[b].e_extracellular(0.5)=(cur1*ext1.x[i]*1e-6)+ (cur2*ext2.x[i]*1e-6)+ (cur3*ext3.x[i]*1e-6) //mV//
        b=b+2
    }
    
    c=0 
    for (i=2;i<=axontotal-1;i=i+stinnum+5) {
        FLUT[c].e_extracellular(0.5)=(cur1*ext1.x[i]*1e-6)+ (cur2*ext2.x[i]*1e-6)+ (cur3*ext3.x[i]*1e-6) //mV//
        c=c+2
    }

    for (j = 0;j<=stinnum-1;j=j+1) {
        d=j 
        for (i=3;i<=axontotal-1;i=i+stinnum+5) {
            STIN[d].e_extracellular(0.5)=(cur1*ext1.x[i+j]*1e-6)+ (cur2*ext2.x[i+j]*1e-6)+ (cur3*ext3.x[i+j]*1e-6) //mV//
            d=d+stinnum
        }
    }

    k=1 
    for (i=9;i<=axontotal-2;i=i+stinnum+5) {
        FLUT[k].e_extracellular(0.5)=(cur1*ext1.x[i]*1e-6)+ (cur2*ext2.x[i]*1e-6)+ (cur3*ext3.x[i]*1e-6) //mV//
        k=k+2
    }
    
    l=1
    for (i=10;i<=axontotal-2;i=i+stinnum+5) {
        MYSA[l].e_extracellular(0.5)=(cur1*ext1.x[i]*1e-6) + (cur2*ext2.x[i]*1e-6)+ (cur3*ext3.x[i]*1e-6)
        l=l+2
    }
}

proc electrode_api_change_pos(){
    // Set electrode position
    // electrode_api_change_pos(ypos, x1, [x2, [x3]])
    yelec = $1
    xelec[0] = $2
    if(numarg() > 2){
        xelec[1] = $3
        if(numarg() > 3){
            xelec[2] = $4
        }else {
            xelec[2] = 0
        }
    } else {
        xelec[1] = 0
        xelec[2] = 0
    }
	for i=0,axontotal-1 {
		ext1.x[i]=(rhoe)/(4*PI*sqrt((yelec^2)+((xelec[0]-x.x[i])^2)))	//nV//
		ext2.x[i]=(rhoe)/(4*PI*sqrt((yelec^2)+((xelec[1]-x.x[i])^2)))	//nV//
		ext3.x[i]=(rhoe)/(4*PI*sqrt((yelec^2)+((xelec[2]-x.x[i])^2)))	//nV//
	}
}

proc electrode_api_setup(){
    amp1 = 0
    sinestim()
    setstim(0,0,0)
    wavesel[1] = 0
}